# Data path
# Input: INOUT, SECTOR
# Segnali di controllo: IS_NIGHT, SAVING_LOCATION
# Output: SETTORE_NON_VALIDO, SBARRA_IN, SBARRA_OUT, SECTOR_A, SECTOR_B, SECTOR_C
# Segnali di stato: TURN_OFF, TURN_ON
.model DATA_PATH
.inputs INOUT1 INOUT0  SECTOR2 SECTOR1 SECTOR0  IS_NIGHT  SAVING_LOCATION1  SAVING_LOCATION0 
.outputs SETTORE_NON_VALIDO  SBARRA_IN  SBARRA_OUT  SECTOR_A  SECTOR_B  SECTOR_C  TURN_OFF TURN_ON

# Definizione costanti
.subckt COSTANTE_0 O=CONST_0
.subckt COSTANTE_1 O=CONST_1
.subckt COSTANTE_31_11111 O4=CONST_31_4 O3=CONST_31_3 O2=CONST_31_2 O1=CONST_31_1 O0=CONST_31_0
.subckt COSTANTE_24_11000  O4=CONST_24_4 O3=CONST_24_3 O2=CONST_24_2 O1=CONST_24_1 O0=CONST_24_0

# Settori
# Poiché il codice per ogni settore è ripetuto con poche modifiche lo abbiamo estratto in un data path separato.
# Ad ogni sottosezione sono passati i segnali necessari e le costanti necessarie.
# O_RECENT=SECTOR_X_AVAILABLE_RECENT: è calcolato usando il valore appena inserito dall'utente
# O_DELAYED=SECTOR_X_AVAILABLE_DELAYED: è calcolato usando il valore del registro, quindi è "in ritardo" di un ciclo
# Dividendoli e usando o l'uno o l'altro dove è opportuno possiamo avere l'output di SECTOR_X aggiornato
# senza incorrere in cicli nel resto del data path.
.subckt SECTOR_SUBSECTION INOUT1=INOUT1 INOUT0=INOUT0  SECTOR2=SECTOR2 SECTOR1=SECTOR1 SECTOR0=SECTOR0  SBARRA_IN=CAN_ENTER  SBARRA_OUT=CAN_EXIT  SAVING_LOCATION1=SAVING_LOCATION1 SAVING_LOCATION0=SAVING_LOCATION0  MAX4=CONST_31_4 MAX3=CONST_31_3 MAX2=CONST_31_2 MAX1=CONST_31_1 MAX0=CONST_31_0  CORRECT_SAVING_LOCATION1=CONST_0 CORRECT_SAVING_LOCATION0=CONST_1  CORRECT_SECTOR2=CONST_1 CORRECT_SECTOR1=CONST_0 CORRECT_SECTOR0=CONST_0  O_DELAYED=SECTOR_A_AVAILABLE O_RECENT=SECTOR_A_AVAILABLE_RECENT
.subckt SECTOR_SUBSECTION INOUT1=INOUT1 INOUT0=INOUT0  SECTOR2=SECTOR2 SECTOR1=SECTOR1 SECTOR0=SECTOR0  SBARRA_IN=CAN_ENTER  SBARRA_OUT=CAN_EXIT  SAVING_LOCATION1=SAVING_LOCATION1 SAVING_LOCATION0=SAVING_LOCATION0  MAX4=CONST_31_4 MAX3=CONST_31_3 MAX2=CONST_31_2 MAX1=CONST_31_1 MAX0=CONST_31_0  CORRECT_SAVING_LOCATION1=CONST_1 CORRECT_SAVING_LOCATION0=CONST_0  CORRECT_SECTOR2=CONST_0 CORRECT_SECTOR1=CONST_1 CORRECT_SECTOR0=CONST_0  O_DELAYED=SECTOR_B_AVAILABLE O_RECENT=SECTOR_B_AVAILABLE_RECENT
.subckt SECTOR_SUBSECTION INOUT1=INOUT1 INOUT0=INOUT0  SECTOR2=SECTOR2 SECTOR1=SECTOR1 SECTOR0=SECTOR0  SBARRA_IN=CAN_ENTER  SBARRA_OUT=CAN_EXIT  SAVING_LOCATION1=SAVING_LOCATION1 SAVING_LOCATION0=SAVING_LOCATION0  MAX4=CONST_24_4 MAX3=CONST_24_3 MAX2=CONST_24_2 MAX1=CONST_24_1 MAX0=CONST_24_0  CORRECT_SAVING_LOCATION1=CONST_1 CORRECT_SAVING_LOCATION0=CONST_1  CORRECT_SECTOR2=CONST_0 CORRECT_SECTOR1=CONST_0 CORRECT_SECTOR0=CONST_1  O_DELAYED=SECTOR_C_AVAILABLE O_RECENT=SECTOR_C_AVAILABLE_RECENT

.subckt NOT A=SECTOR_A_AVAILABLE_RECENT O=SECTOR_A
.subckt NOT A=SECTOR_B_AVAILABLE_RECENT O=SECTOR_B
.subckt NOT A=SECTOR_C_AVAILABLE_RECENT O=SECTOR_C

# TURN_OFF e TURN_ON
.subckt IS_TURN_OFF A4=INOUT1 A3=INOUT0 A2=SECTOR2 A1=SECTOR1 A0=SECTOR0  O=TURN_OFF
.subckt IS_TURN_ON A4=INOUT1 A3=INOUT0 A2=SECTOR2 A1=SECTOR1 A0=SECTOR0  O=TURN_ON

# SBARRA_IN
# 000 (001) (010) 011 (100) 101 110 111
.subckt MUX_S3_1 S2=SECTOR2 S1=SECTOR1 S0=SECTOR0  A=CONST_0 B=SECTOR_C_AVAILABLE C=SECTOR_B_AVAILABLE D=CONST_0 E=SECTOR_A_AVAILABLE F=CONST_0 G=CONST_0 H=CONST_0  O=SECTOR_AVAILABLE
.subckt MUX_S2_1 S1=INOUT1 S0=INOUT0  A=CONST_0 B=SECTOR_AVAILABLE C=CONST_0 D=CONST_0  O=CAN_ENTER
.subckt MUX_S1_1 S=IS_NIGHT  A=CAN_ENTER B=CONST_1  O=SBARRA_IN

# SETTORE_NON_VALIDO
.subckt IS_VALID_SECTOR A2=SECTOR2 A1=SECTOR1 A0=SECTOR0  O=SETTORE_VALIDO
.subckt NOT A=SETTORE_VALIDO  O=SETTORE_NON_VALIDO_TEMP
.subckt MUX_S1_1 S=IS_NIGHT  A=SETTORE_NON_VALIDO_TEMP B=CONST_0  O=SETTORE_NON_VALIDO

# SBARRA_OUT
.subckt MUX_S2_1 S1=INOUT1 S0=INOUT0  A=CONST_0 B=CONST_0 C=SETTORE_VALIDO D=CONST_0  O=CAN_EXIT
.subckt MUX_S1_1 S=IS_NIGHT A=CAN_EXIT B=CONST_1  O=SBARRA_OUT

.search flusso/mux/MUX_S1_1.blif
.search flusso/mux/MUX_S2_1.blif
.search flusso/mux/MUX_S3_1.blif
.search flusso/IS_TURN_OFF.blif
.search flusso/IS_TURN_ON.blif
.search flusso/IS_VALID_SECTOR.blif
.search operatori/NOT.blif
.search costanti/COSTANTE_0.blif
.search costanti/COSTANTE_1.blif
.search costanti/COSTANTE_24_11000.blif
.search costanti/COSTANTE_31_11111.blif
.search SECTOR_SUBSECTION.blif

.end
