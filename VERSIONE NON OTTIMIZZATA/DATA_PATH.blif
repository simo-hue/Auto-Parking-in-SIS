# Data path
# Input: INOUT, SECTOR
# Segnali di controllo: NOT_OPER, SAVING_LOCATION
# Output: SETTORE_NON_VALIDO, SB_IN, SB_OUT, SECTOR_A, SECTOR_B, SECTOR_C
# Segnali di stato: PWR_STATUS_OFF_T, PWR_STATUS_ON_T
.model DATA_PATH
.inputs IO1 IO0  S2 S1 S0  NOT_OPER  SALVA_1  SALVA_0 
.outputs SETTORE_NON_VALIDO  SB_IN  SB_OUT  SECTOR_A  SECTOR_B  SECTOR_C  PWR_STATUS_OFF_T PWR_STATUS_ON_T

# Definizione costanti
.subckt C_0 O=C_0
.subckt C_1 O=C_1
.subckt C_31 O4=CONST_31_4 O3=CONST_31_3 O2=CONST_31_2 O1=CONST_31_1 O0=CONST_31_0
.subckt C_24 O4=CONST_24_4 O3=CONST_24_3 O2=CONST_24_2 O1=CONST_24_1 O0=CONST_24_0

# Settori
# Poiché il codice per ogni settore è ripetuto con poche modifiche lo abbiamo estratto in un data path separato.
# Ad ogni sottosezione sono passati i segnali necessari e le costanti necessarie.
# REC=SECTOR_X_AVAILABLE_RECENT: è calcolato usando il valore appena inserito dall'utente
# RIT=SECTOR_X_AVAILABLE_DELAYED: è calcolato usando il valore del registro, quindi è "in ritardo" di un ciclo
# Dividendoli e usando o l'uno o l'altro dove è opportuno possiamo avere l'output di SECTOR_X aggiornato
# senza incorrere in cicli nel resto del data path.
.subckt S_PARCHEGGIO IO1=IO1 IO0=IO0  S2=S2 S1=S1 S0=S0  SB_IN=CAN_ENTER  SB_OUT=CAN_EXIT  SALVA_1=SALVA_1 SALVA_0=SALVA_0  MAX4=CONST_31_4 MAX3=CONST_31_3 MAX2=CONST_31_2 MAX1=CONST_31_1 MAX0=CONST_31_0  SAL_COR_1=C_0 SAL_COR_0=C_1  SET_COR_2=C_1 SET_COR_1=C_0 SET_COR_0=C_0  RIT=SECTOR_A_AVAILABLE REC=SECTOR_A_AVAILABLE_RECENT
.subckt S_PARCHEGGIO IO1=IO1 IO0=IO0  S2=S2 S1=S1 S0=S0  SB_IN=CAN_ENTER  SB_OUT=CAN_EXIT  SALVA_1=SALVA_1 SALVA_0=SALVA_0  MAX4=CONST_31_4 MAX3=CONST_31_3 MAX2=CONST_31_2 MAX1=CONST_31_1 MAX0=CONST_31_0  SAL_COR_1=C_1 SAL_COR_0=C_0  SET_COR_2=C_0 SET_COR_1=C_1 SET_COR_0=C_0  RIT=SECTOR_B_AVAILABLE REC=SECTOR_B_AVAILABLE_RECENT
.subckt S_PARCHEGGIO IO1=IO1 IO0=IO0  S2=S2 S1=S1 S0=S0  SB_IN=CAN_ENTER  SB_OUT=CAN_EXIT  SALVA_1=SALVA_1 SALVA_0=SALVA_0  MAX4=CONST_24_4 MAX3=CONST_24_3 MAX2=CONST_24_2 MAX1=CONST_24_1 MAX0=CONST_24_0  SAL_COR_1=C_1 SAL_COR_0=C_1  SET_COR_2=C_0 SET_COR_1=C_0 SET_COR_0=C_1  RIT=SECTOR_C_AVAILABLE REC=SECTOR_C_AVAILABLE_RECENT

.subckt NOT A=SECTOR_A_AVAILABLE_RECENT O=SECTOR_A
.subckt NOT A=SECTOR_B_AVAILABLE_RECENT O=SECTOR_B
.subckt NOT A=SECTOR_C_AVAILABLE_RECENT O=SECTOR_C

# PWR_STATUS_OFF_T e PWR_STATUS_ON_T
.subckt OFF A4=IO1 A3=IO0 A2=S2 A1=S1 A0=S0  O=PWR_STATUS_OFF_T
.subckt ON A4=IO1 A3=IO0 A2=S2 A1=S1 A0=S0  O=PWR_STATUS_ON_T

# SB_IN
# 000 (001) (010) 011 (100) 101 110 111
.subckt MUX_3_1 S2=S2 S1=S1 S0=S0  A=C_0 B=SECTOR_C_AVAILABLE C=SECTOR_B_AVAILABLE D=C_0 E=SECTOR_A_AVAILABLE F=C_0 G=C_0 H=C_0  O=SECTOR_AVAILABLE
.subckt MUX_2_1 S1=IO1 S0=IO0  A=C_0 B=SECTOR_AVAILABLE C=C_0 D=C_0  O=CAN_ENTER
.subckt MUX_1_1 S=NOT_OPER  A=CAN_ENTER B=C_1  O=SB_IN

# SETTORE_NON_VALIDO
.subckt VALIDO A2=S2 A1=S1 A0=S0  O=SETTORE_VALIDO
.subckt NOT A=SETTORE_VALIDO  O=SETTORE_NON_VALIDO_TEMP
.subckt MUX_1_1 S=NOT_OPER  A=SETTORE_NON_VALIDO_TEMP B=C_0  O=SETTORE_NON_VALIDO

# SB_OUT
.subckt MUX_2_1 S1=IO1 S0=IO0  A=C_0 B=C_0 C=SETTORE_VALIDO D=C_0  O=CAN_EXIT
.subckt MUX_1_1 S=NOT_OPER A=CAN_EXIT B=C_1  O=SB_OUT

.search MULTIPLEXER/MUX_1_1.blif
.search MULTIPLEXER/MUX_2_1.blif
.search MULTIPLEXER/MUX_3_1.blif
.search CONTROLLI/OFF.blif
.search CONTROLLI/ON.blif
.search CONTROLLI/VALIDO.blif
.search OPERATORI/NOT.blif
.search COSTANTI/C_0.blif
.search COSTANTI/C_1.blif
.search COSTANTI/C_24.blif
.search COSTANTI/C_31.blif
.search S_PARCHEGGIO.blif

.end
