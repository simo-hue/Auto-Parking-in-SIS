# Rappresenta un settore del parcheggio.
# Incrementa e decrementa automaticamente il numero di auto nel settore in base alla situazione.
# Gesticsce l'inizializzazione del numero di auto nel settore all'accensione.
# Restituisce se ci sono ancora posti disponibili o no.
# MAX: il valore massimo raggiungibile nel parcheggio
# CORRECT_SAVING_LOCATION: la sequenza necessaria per salvare il valore qua
# CORRECT_SECTOR: il valore che SECTOR deve avere per provare ad entrare (+1 nel registro) ed uscire (-1 nel registro) qui
# O_DELAYED: 1 se il settore è pieno; 0 altrimenti. È calcolato con il valore del registro, in "ritardo" di un ciclo.
# Il suo utilizzo permette di evitare cicli nel data path.
# O_RECENT: 1 se il settore è pieno; 0 altrimenti. È calcolato con il valore appena modificato dall'utente.
# Il suo utilizzo permette di avere il risultato aggiornato senza un delay di un ciclo.
.model SECTOR_SUBSECTION
.inputs INOUT1 INOUT0  SECTOR2 SECTOR1 SECTOR0  SBARRA_IN  SBARRA_OUT  SAVING_LOCATION1 SAVING_LOCATION0  MAX4 MAX3 MAX2 MAX1 MAX0  CORRECT_SAVING_LOCATION1 CORRECT_SAVING_LOCATION0  CORRECT_SECTOR2 CORRECT_SECTOR1 CORRECT_SECTOR0
.outputs O_DELAYED O_RECENT

# Costanti
.subckt COSTANTE_0 O=CONST_0
.subckt COSTANTE_1 O=CONST_1

# Registro
.subckt REGISTRO_5 A4=REG_INPUT4 A3=REG_INPUT3 A2=REG_INPUT2 A1=REG_INPUT1 A0=REG_INPUT0  O4=REG_OUTPUT4 O3=REG_OUTPUT3 O2=REG_OUTPUT2 O1=REG_OUTPUT1 O0=REG_OUTPUT0

# Se stai uscendo, sottrai 1 al valore del registro. Restituisci RES_SBARRA_OUT_MUX
.subckt MASSIMO_5 A4=REG_OUTPUT4 A3=REG_OUTPUT3 A2=REG_OUTPUT2 A1=REG_OUTPUT1 A0=REG_OUTPUT0  B4=CONST_0 B3=CONST_0 B2=CONST_0 B1=CONST_0 B0=CONST_1  O4=SOTTRATTORE_INPUT4 O3=SOTTRATTORE_INPUT3 O2=SOTTRATTORE_INPUT2 O1=SOTTRATTORE_INPUT1 O0=SOTTRATTORE_INPUT0 
.subckt DECREMENTATORE_5 A4=SOTTRATTORE_INPUT4 A3=SOTTRATTORE_INPUT3 A2=SOTTRATTORE_INPUT2 A1=SOTTRATTORE_INPUT1 A0=SOTTRATTORE_INPUT0  O4=RES_DIFFERENZA4 O3=RES_DIFFERENZA3 O2=RES_DIFFERENZA2 O1=RES_DIFFERENZA1 O0=RES_DIFFERENZA0
.subckt MUX_S1_5 S=SBARRA_OUT  A4=REG_OUTPUT4 A3=REG_OUTPUT3 A2=REG_OUTPUT2 A1=REG_OUTPUT1 A0=REG_OUTPUT0  B4=RES_DIFFERENZA4 B3=RES_DIFFERENZA3 B2=RES_DIFFERENZA2 B1=RES_DIFFERENZA1 B0=RES_DIFFERENZA0  O4=RES_SBARRA_OUT_MUX4 O3=RES_SBARRA_OUT_MUX3 O2=RES_SBARRA_OUT_MUX2 O1=RES_SBARRA_OUT_MUX1 O0=RES_SBARRA_OUT_MUX0

# Se stai entrando, somma 1 a RES_SBARRA_OUT_MUX. Restituisci RES_SBARRA_IN_MUX
.subckt INCREMENTATORE_5 A4=REG_OUTPUT4 A3=REG_OUTPUT3 A2=REG_OUTPUT2 A1=REG_OUTPUT1 A0=REG_OUTPUT0  O4=RES_SOMMA4 O3=RES_SOMMA3 O2=RES_SOMMA2 O1=RES_SOMMA1 O0=RES_SOMMA0
.subckt MUX_S1_5 S=SBARRA_IN  A4=RES_SBARRA_OUT_MUX4 A3=RES_SBARRA_OUT_MUX3 A2=RES_SBARRA_OUT_MUX2 A1=RES_SBARRA_OUT_MUX1 A0=RES_SBARRA_OUT_MUX0  B4=RES_SOMMA4 B3=RES_SOMMA3 B2=RES_SOMMA2 B1=RES_SOMMA1 B0=RES_SOMMA0  O4=RES_SBARRA_IN_MUX4 O3=RES_SBARRA_IN_MUX3 O2=RES_SBARRA_IN_MUX2 O1=RES_SBARRA_IN_MUX1 O0=RES_SBARRA_IN_MUX0

# Procedere solo se SECTOR==CORRECT_SECTOR
.subckt UGUALE_3 A2=SECTOR2 A1=SECTOR1 A0=SECTOR0  B2=CORRECT_SECTOR2 B1=CORRECT_SECTOR1 B0=CORRECT_SECTOR0  O=IS_CORRECT_SECTOR
.subckt MUX_S1_5 S=IS_CORRECT_SECTOR  A4=REG_OUTPUT4 A3=REG_OUTPUT3 A2=REG_OUTPUT2 A1=REG_OUTPUT1 A0=REG_OUTPUT0  B4=RES_SBARRA_IN_MUX4 B3=RES_SBARRA_IN_MUX3 B2=RES_SBARRA_IN_MUX2 B1=RES_SBARRA_IN_MUX1 B0=RES_SBARRA_IN_MUX0  O4=RES_CORRECT_SECTOR4 O3=RES_CORRECT_SECTOR3 O2=RES_CORRECT_SECTOR2 O1=RES_CORRECT_SECTOR1 O0=RES_CORRECT_SECTOR0

# Se SAVING_LOCATION==CORRECT_SAVING_LOCATION, salva il valore di (INOUT e SECTOR) nel registro.
# Altrimenti salvagli il valore delle operazioni precedenti.
.subckt MINIMO_5 A4=INOUT1 A3=INOUT0 A2=SECTOR2 A1=SECTOR1 A0=SECTOR0  B4=MAX4 B3=MAX3 B2=MAX2 B1=MAX1 B0=MAX0  O4=SAVED_VALUE4 O3=SAVED_VALUE3 O2=SAVED_VALUE2 O1=SAVED_VALUE1 O0=SAVED_VALUE0
.subckt UGUALE_2 A1=SAVING_LOCATION1 A0=SAVING_LOCATION0  B1=CORRECT_SAVING_LOCATION1 B0=CORRECT_SAVING_LOCATION0  O=IS_SAVING_HERE
.subckt MUX_S1_5 S=IS_SAVING_HERE  A4=RES_CORRECT_SECTOR4 A3=RES_CORRECT_SECTOR3 A2=RES_CORRECT_SECTOR2 A1=RES_CORRECT_SECTOR1 A0=RES_CORRECT_SECTOR0  B4=SAVED_VALUE4 B3=SAVED_VALUE3 B2=SAVED_VALUE2 B1=SAVED_VALUE1 B0=SAVED_VALUE0  O4=REG_INPUT4 O3=REG_INPUT3 O2=REG_INPUT2 O1=REG_INPUT1 O0=REG_INPUT0

# Minori finali, uno per O_DELAYED e uno per O_RECENT (nota la differenza negli argomenti di A)
.subckt MINORE_5 A4=REG_OUTPUT4 A3=REG_OUTPUT3 A2=REG_OUTPUT2 A1=REG_OUTPUT1 A0=REG_OUTPUT0  B4=MAX4 B3=MAX3 B2=MAX2 B1=MAX1 B0=MAX0  O=O_DELAYED
.subckt MINORE_5 A4=REG_INPUT4 A3=REG_INPUT3 A2=REG_INPUT2 A1=REG_INPUT1 A0=REG_INPUT0  B4=MAX4 B3=MAX3 B2=MAX2 B1=MAX1 B0=MAX0  O=O_RECENT

.search registri/REGISTRO_5.blif
.search flusso/mux/MUX_S1_5.blif
.search operatori/INCREMENTATORE_5.blif
.search operatori/DECREMENTATORE_5.blif
.search operatori/UGUALE_2.blif
.search operatori/UGUALE_3.blif
.search operatori/MINORE_5.blif
.search operatori/MINIMO_5.blif
.search operatori/MASSIMO_5.blif
.search costanti/COSTANTE_0.blif
.search costanti/COSTANTE_1.blif

.end
