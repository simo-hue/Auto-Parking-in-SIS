# Rappresenta un settore del parcheggio.
# Incrementa e decrementa automaticamente il numero di auto nel settore in base alla situazione.
# Gesticsce l'inizializzazione del numero di auto nel settore all'accensione.
# Restituisce se ci sono ancora posti disponibili o no.
# MAX: il valore massimo raggiungibile nel parcheggio
# CORRECT_SAVING_LOCATION: la sequenza necessaria per salvare il valore qua
# CORRECT_SECTOR: il valore che SECTOR deve avere per provare ad entrare (+1 nel registro) ed uscire (-1 nel registro) qui
# RIT: 1 se il settore è pieno; 0 altrimenti. È calcolato con il valore del registro, in "ritardo" di un ciclo.
# Il suo utilizzo permette di evitare cicli nel data path.
# REC: 1 se il settore è pieno; 0 altrimenti. È calcolato con il valore appena modificato dall'utente.
# Il suo utilizzo permette di avere il risultato aggiornato senza un delay di un ciclo.
.model S_PARCHEGGIO
.inputs IO1 IO0  S2 S1 S0  SB_IN  SB_OUT  SALVA_1 SALVA_0 MAX4 MAX3 MAX2 MAX1 MAX0  SAL_COR_1 SAL_COR_0  SET_COR_2 SET_COR_1 SET_COR_0
.outputs RIT REC

# Costanti
.subckt C_0 O=C_0
.subckt C_1 O=C_1

# Registro
.subckt REG_5 A4=REG_INPUT4 A3=REG_INPUT3 A2=REG_INPUT2 A1=REG_INPUT1 A0=REG_INPUT0  O4=REG_OUTPUT4 O3=REG_OUTPUT3 O2=REG_OUTPUT2 O1=REG_OUTPUT1 O0=REG_OUTPUT0


# Se stai uscendo, sottrai 1 al valore del registro. Restituisci RES_SB_OUT_MUX
.subckt MAX_5 A4=REG_OUTPUT4 A3=REG_OUTPUT3 A2=REG_OUTPUT2 A1=REG_OUTPUT1 A0=REG_OUTPUT0  B4=C_0 B3=C_0 B2=C_0 B1=C_0 B0=C_1  O4=SOT_INPUT4 O3=SOT_INPUT3 O2=SOT_INPUT2 O1=SOT_INPUT1 O0=SOT_INPUT0 

.subckt DEC_5 A4=SOT_INPUT4 A3=SOT_INPUT3 A2=SOT_INPUT2 A1=SOT_INPUT1 A0=SOT_INPUT0  O4=RES_DIF4 O3=RES_DIF3 O2=RES_DIF2 O1=RES_DIF1 O0=RES_DIF0
.subckt MUX_1_5 S=SB_OUT  A4=REG_OUTPUT4 A3=REG_OUTPUT3 A2=REG_OUTPUT2 A1=REG_OUTPUT1 A0=REG_OUTPUT0  B4=RES_DIF4 B3=RES_DIF3 B2=RES_DIF2 B1=RES_DIF1 B0=RES_DIF0  O4=RSOM4 O3=RSOM3 O2=RSOM2 O1=RSOM1 O0=RSOM0

# Se stai entrando, somma 1 a RES_SB_OUT_MUX. Restituisci RSIM
.subckt INC_5 A4=REG_OUTPUT4 A3=REG_OUTPUT3 A2=REG_OUTPUT2 A1=REG_OUTPUT1 A0=REG_OUTPUT0  O4=SOM4 O3=SOM3 O2=SOM2 O1=SOM1 O0=SOM0
.subckt MUX_1_5 S=SB_IN  A4=RSOM4 A3=RSOM3 A2=RSOM2 A1=RSOM1 A0=RSOM0  B4=SOM4 B3=SOM3 B2=SOM2 B1=SOM1 B0=SOM0  O4=RSIM4 O3=RSIM3 O2=RSIM2 O1=RSIM1 O0=RSIM0

# Procedere solo se SECTOR==CORRECT_SECTOR
.subckt EQ_3 A2=S2 A1=S1 A0=S0  B2=SET_COR_2 B1=SET_COR_1 B0=SET_COR_0  O=IS_CORRECT_SECTOR
.subckt MUX_1_5 S=IS_CORRECT_SECTOR  A4=REG_OUTPUT4 A3=REG_OUTPUT3 A2=REG_OUTPUT2 A1=REG_OUTPUT1 A0=REG_OUTPUT0  B4=RSIM4 B3=RSIM3 B2=RSIM2 B1=RSIM1 B0=RSIM0  O4=SEC_COR_4 O3=SEC_COR_3 O2=SEC_COR_2 O1=SEC_COR_1 O0=SEC_COR_0

# Se SAVING_LOCATION==CORRECT_SAVING_LOCATION, salva il valore di (INOUT e SECTOR) nel registro.
# Altrimenti salvagli il valore delle operazioni precedenti.
.subckt MIN_5 A4=IO1 A3=IO0 A2=S2 A1=S1 A0=S0  B4=MAX4 B3=MAX3 B2=MAX2 B1=MAX1 B0=MAX0  O4=SAVED_VALUE4 O3=SAVED_VALUE3 O2=SAVED_VALUE2 O1=SAVED_VALUE1 O0=SAVED_VALUE0
.subckt EQ_2 A1=SALVA_1 A0=SALVA_0  B1=SAL_COR_1 B0=SAL_COR_0  O=IS_SAVING_HERE
.subckt MUX_1_5 S=IS_SAVING_HERE  A4=SEC_COR_4 A3=SEC_COR_3 A2=SEC_COR_2 A1=SEC_COR_1 A0=SEC_COR_0  B4=SAVED_VALUE4 B3=SAVED_VALUE3 B2=SAVED_VALUE2 B1=SAVED_VALUE1 B0=SAVED_VALUE0  O4=REG_INPUT4 O3=REG_INPUT3 O2=REG_INPUT2 O1=REG_INPUT1 O0=REG_INPUT0

# Minori finali, uno per RIT e uno per REC (nota la differenza negli argomenti di A)
.subckt MIN_5_1 A4=REG_OUTPUT4 A3=REG_OUTPUT3 A2=REG_OUTPUT2 A1=REG_OUTPUT1 A0=REG_OUTPUT0  B4=MAX4 B3=MAX3 B2=MAX2 B1=MAX1 B0=MAX0  O=RIT
.subckt MIN_5_1 A4=REG_INPUT4 A3=REG_INPUT3 A2=REG_INPUT2 A1=REG_INPUT1 A0=REG_INPUT0  B4=MAX4 B3=MAX3 B2=MAX2 B1=MAX1 B0=MAX0  O=REC

.search REGISTRI/REG_5.blif
.search MULTIPLEXER/MUX_1_5.blif
.search OPERATORI/INC_5.blif
.search OPERATORI/DEC_5.blif
.search OPERATORI/EQ_2.blif
.search OPERATORI/EQ_3.blif
.search OPERATORI/MIN_5_1.blif
.search OPERATORI/MIN_5.blif
.search OPERATORI/MAX_5.blif
.search COSTANTI/C_0.blif
.search COSTANTI/C_1.blif

.end
